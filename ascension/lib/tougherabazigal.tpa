//Copy and Compile
COPY ~ascension/tougher/abazigal/cre~ ~override~
COPY ~ascension/tougher/abazigal/itm~ ~override~
COPY ~ascension/tougher/abazigal/spl~ ~override~
COPY ~ascension/ascensionmain/spl/balortk2.SPL~ ~override/balortk2.spl~ SAY 8 @1009
COMPILE ~ascension/tougher/abazigal/baf/compile~

//Area Patching
COPY_EXISTING ~ar6005.are~ ~override~
	READ_LONG  0x54  "actors_offset"
	READ_SHORT 0x58  "actors_num"

	FOR( cnt=0; cnt<"%actors_num%"; cnt=cnt+1 ) BEGIN  //actors loop
		READ_ASCII ("%actors_offset%"+0x110*cnt)       "actor_name" (8)
		PATCH_IF (("%actor_name%" STRING_COMPARE_CASE "Abazigal")=0) BEGIN
			SET offset = ("%actors_offset%"+0x110*cnt)
			WRITE_SHORT (offset+0x20) 1912         //X
			WRITE_SHORT (offset+0x22) 1207         //Y
			WRITE_SHORT (offset+0x24) 1912         //destination X
			WRITE_SHORT (offset+0x26) 1207         //destination Y
			WRITE_SHORT (offset+0x34) 6
  	END	
	END
BUT_ONLY

//TOUGHER ABAZIGAL CRE PATCHING
COPY_EXISTING ~ABAZIGAL.CRE~ ~override~      //Abazigal >:)>
 WRITE_BYTE 0x6e 0        //Unused Large Prof
 WRITE_BYTE 0x6f 0        //Unused Small Prof
 WRITE_BYTE 0x70 0        //Unused Bow Prof
 WRITE_BYTE 0x71 0        //Unused Spear Prof
 WRITE_BYTE 0x72 0        //Unused Blunt Prof
 WRITE_BYTE 0x73 0        //Unused Spiked Prof
 WRITE_BYTE 0x74 0        //Unused Axe Prof
 WRITE_BYTE 0x270 255     //Allegiance
 
 LPF DELETE_EFFECT //increase attack speed
 INT_VAR
  match_opcode = 190
 END
 
 LPF DELETE_EFFECT //backstab immunity
 INT_VAR
  match_opcode = 292
 END
 
 LPF ADD_CRE_EFFECT //movement rate bonus
 INT_VAR
  opcode = 126
  timing = 1
  parameter1 = 3
  probability1 = 100
 END
 
 LPF ADD_CRE_EFFECT //regeneration
 INT_VAR
  opcode = 98
  parameter1 = 5
  parameter2 = 2
  timing = 1
  probability1 = 100
 END
 
 LPF ADD_CRE_EFFECT //timestop immunity
 INT_VAR
  opcode = 310
  timing = 1
  probability1 = 100
 END
 
 LPF ADD_CRE_EFFECT //immunity to mindflayer cheat
 INT_VAR
  opcode = 101
  parameter2 = 19
  timing = 1
  probability1 = 100
 END
 
 LPF ADD_CRE_EFFECT //immunity to wing buffet
 INT_VAR
  opcode = 101
  parameter2 = 235
  timing = 1
  probability1 = 100
 END
BUT_ONLY

COPY_EXISTING ~DRAGBLUE.CRE~ ~override~   //Abazigal
 WRITE_LONG 0x14 78000      //XP Value
 WRITE_SHORT 0x24 500       //Current HP
 WRITE_SHORT 0x26 500       //Max HP
 WRITE_SHORT 0x52 (0 - 10)  //THAC0
 WRITE_BYTE 0x59 50         //Resist Fire
 WRITE_BYTE 0x5a 50         //Resist Cold
 WRITE_BYTE 0x5b 127        //Resist Electricity
 WRITE_BYTE 0x5c 50         //Resist Acid
 WRITE_BYTE 0x5d 65         //Resist Magic
 WRITE_BYTE 0x5e 50         //Resist magic fire
 WRITE_BYTE 0x5f 50         //Resist magic cold
 WRITE_BYTE 0x60 50         //Resist slashing
 WRITE_BYTE 0x61 50         //Resist crushing
 WRITE_BYTE 0x62 50         //Resist piercing
 WRITE_BYTE 0x63 50         //Resist missile
 WRITE_BYTE 0x6e 0          //Unused Large Prof
 WRITE_BYTE 0x6f 0          //Unused Small Prof
 WRITE_BYTE 0x70 0          //Unused Bow Prof
 WRITE_BYTE 0x71 0          //Unused Spear Prof
 WRITE_BYTE 0x72 0          //Unused Blunt Prof
 WRITE_BYTE 0x73 0          //Unused Spiked Prof
 WRITE_BYTE 0x74 0          //Unused Axe Prof
 
 LPF ADD_CRE_EFFECT //Magic Damage Resist
 INT_VAR
  opcode = 31
  parameter1 = 65
  parameter2 = 1
  timing = 9
  probability1 = 100
 END
 
 LPF ADD_CRE_EFFECT //protection from spell
 INT_VAR
  opcode = 206
  timing = 1
  probability1 = 100
  STR_VAR
  resource = ABAZDG02
 END
 
 LPF ADD_CRE_EFFECT //immunity to int drain
 INT_VAR
  opcode = 101
  parameter2 = 19
  timing = 1
  probability1 = 100
 END
BUT_ONLY
 
COPY_EXISTING ~SALGRICE.CRE~ ~override~  //Salamander
 WRITE_LONG 0x14 7500     //XP value
 WRITE_SHORT 0x24 120     //Current HP
 WRITE_SHORT 0x26 120     //Max HP
 WRITE_BYTE 0x52 7        //THAC0
 WRITE_BYTE 0x53 3        //# of Attacks
 WRITE_BYTE 0x54 6        //Save vs death
 WRITE_BYTE 0x55 8        //Save vs wand
 WRITE_BYTE 0x56 7        //Save vs Poly
 WRITE_BYTE 0x57 7        //Save vs Breath
 WRITE_BYTE 0x58 9        //Save vs Spell
 WRITE_BYTE 0x59 (0 - 25) //Fire vulnerability
 WRITE_BYTE 0x5a 125      //Cold Resistance
 WRITE_BYTE 0x5b 50       //Electricity resistance
 WRITE_BYTE 0x5e (0 - 25) //Magic Fire Vulnerability
 WRITE_BYTE 0x5f 125      //Magic Cold Resistance
 WRITE_BYTE 0x6e 0        //Unused Large Prof
 WRITE_BYTE 0x6f 0        //Unused Small Prof
 WRITE_BYTE 0x70 0        //Unused Bow Prof
 WRITE_BYTE 0x71 0        //Unused Spear Prof
 WRITE_BYTE 0x72 0        //Unused Blunt Prof
 WRITE_BYTE 0x73 0        //Unused Spiked Prof
 WRITE_BYTE 0x74 0        //Unused Axe Prof
 WRITE_BYTE 0x234 17      //Highest Level 1st Class
 WRITE_BYTE 0x235 0       //Highest Level 2nd Class
 WRITE_BYTE 0x236 0       //Highest Level 3rd Class

//ITM PATCHING

COPY_EXISTING ~DRAGBLUE.ITM~ ~override~
 LPF ADD_ITEM_EFFECT //wingbuffet
 INT_VAR
  opcode = 235
  target = 2
  parameter1 = 5
  parameter2 = 2
  dispel_resist = 2
  duration = 1
  probability1 = 40
  savingthrow = BIT2
  savebonus = (0 - 4)
 END
 
 LPF ADD_ITEM_EFFECT  //sleep
 INT_VAR
  opcode = 39
  target = 2
  dispel_resist = 2
  duration = 2
  probability1 = 40
  savingthrow = BIT2
 END
 
 LPF ADD_ITEM_EFFECT //damage
 INT_VAR
  opcode = 12
  target = 2
  timing = 4
  dispel_resist = 2
  duration = 1
  probability1 = 40
  dicenumber = 1
  dicesize = 6
  savingthrow = BIT2
 END
BUT_ONLY

//SPL PATCHING
COPY_EXISTING ~SPIN695.SPL~ ~override~
 WRITE_BYTE 0x25 1         //Primary Type
 WRITE_BYTE 0x27 4         //Secondary Type

 LPF ALTER_EFFECT
 INT_VAR
  check_globals = 1
  match_opcode = 273
  power = 3
 END
BUT_ONLY

//SCRIPT PATCHING
//Abazdrag.bcs
COPY_EXISTING ~ABAZDRAG.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~SetGlobalTimer("WINGBUFFET","LOCALS",8)[%TAB% %LNL%%MNL%%WNL%]+ForceSpell(NearestEnemyOf(Myself),DRAGON_WING_BUFFET)[%TAB% %LNL%%MNL%%WNL%]+Wait(1)~ ~SetGlobalTimer("WINGBUFFET","LOCALS",8) ForceSpell(NearestEnemyOf(Myself),DRAGON_WING_BUFFET) Wait(1) ApplySpell(Myself,0)~
    REPLACE_TEXTUALLY ~!CheckStatGT(Myself,0,STONESKINS)~ ~~
    REPLACE_TEXTUALLY ~True()~ ~ActionListEmpty() !HPPercentLT(Myself,40)~
    REPLACE_TEXTUALLY ~EquipMostDamagingMelee()~ ~~
  END
  R_B_B ~ascension/tougher/abazigal/baf/ABAZDRAG_o1.baf~   ~ascension/tougher/abazigal/baf/ABAZDRAG_n1.baf~
  PATCH_IF GAME_IS ~tob~ THEN BEGIN
    R_B_B ~ascension/tougher/abazigal/baf/ABAZDRAG_o2.baf~ ~ascension/tougher/abazigal/baf/ABAZDRAG_n2.baf~ // remove ReallyForceSpell(LastSeenBy(Myself),0) - only present in ToB classic
  END
  R_B_B ~ascension/tougher/abazigal/baf/ABAZDRAG_o3.baf~   ~ascension/tougher/abazigal/baf/ABAZDRAG_n3.baf~
  R_B_B ~ascension/tougher/abazigal/baf/ABAZDRAG_o4.baf~   ~ascension/tougher/abazigal/baf/ABAZDRAG_n4.baf~
  R_B_B ~ascension/tougher/abazigal/baf/ABAZDRAG_o5.baf~   ~ascension/tougher/abazigal/baf/ABAZDRAG_n5.baf~
  R_B_B ~ascension/tougher/abazigal/baf/ABAZDRAG_o6.baf~   ~ascension/tougher/abazigal/baf/ABAZDRAG_n6.baf~
  R_B_B ~ascension/tougher/abazigal/baf/ABAZDRAG_o7.baf~   ~ascension/tougher/abazigal/baf/ABAZDRAG_n7.baf~

EXTEND_BOTTOM ~ABAZDRAG.bcs~ ~ascension/tougher/abazigal/baf/ABAZDRAG_bottom.baf~

//Abaz2.bcs
COPY_EXISTING ~abaz2.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~!GlobalTimerNotExpired("Hardiness","LOCALS")~ ~See(NearestEnemyOf(Myself)) !GlobalTimerNotExpired("Hardiness","LOCALS")~
    REPLACE_TEXTUALLY ~See(LastAttackerOf(Myself))~ ~See(LastAttackerOf(Myself)) !CheckStatGT(LastSeenBy(Myself),25,INT) !Gender(LastSeenBy(Myself),ILLUSIONARY)~
    REPLACE_TEXTUALLY ~Range(NearestEnemyOf(Myself),4)[%TAB% %LNL%%MNL%%WNL%]+See(NearestEnemyOf(Myself))~ ~Range(NearestEnemyOf(Myself),4) See(NearestEnemyOf(Myself)) !CheckStatGT(LastSeenBy(Myself),25,INT) !Gender(LastSeenBy(Myself),ILLUSIONARY)~
    REPLACE_TEXTUALLY ~AttackReevaluate(LastSeenBy(Myself),45)~ ~~
    REPLACE_TEXTUALLY ~True()~ ~GlobalTimerNotExpired("KaiAttack","LOCALS") !HPPercentLT(Myself,50) RandomNum(2,1)~
    REPLACE_TEXTUALLY ~EquipMostDamagingMelee()~ ~~
  END
  R_B_B ~ascension/tougher/abazigal/baf/abaz2_o1.baf~ ~ascension/tougher/abazigal/baf/abaz2_n1.baf~

EXTEND_BOTTOM ~abaz2.bcs~ ~ascension/tougher/abazigal/baf/abaz2_bottom.baf~

//ar6005.bcs
COPY_EXISTING ~ar6005.bcs~ ~override~
  DECOMPILE_AND_PATCH BEGIN
    REPLACE_TEXTUALLY ~Dead("Abazigal")~ ~Dead("Abazigal") CombatCounter(0)~
  END

EXTEND_TOP ~ar6005.bcs~ ~ascension/tougher/abazigal/baf/ar6005_Top.baf~ EVALUATE_BUFFER
