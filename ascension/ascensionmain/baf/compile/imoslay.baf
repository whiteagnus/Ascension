IF                                        //All enemies are dead, Imoen resolved, Balthazar Dead
  Die()
  Global("JMT#WV1","AR6200",3)            //Current first wave count
  OR(2)
  Global("BalthazarFights","GLOBAL",0)    //Balthazar did not fight with you
  StateCheck("balth2",15)                 //Balthazar died
  !Exists([ENEMY])                        //No summons around
THEN
  RESPONSE #100
    SetGlobal("FinalFight","AR6200",4)     //Set to pre-bridge dialog
END

IF                                     //All enemies are dead, Imoen resolved, Balthazar is ally
  DIE()
  Global("JMT#WV1","AR6200",3)         
  Global("BalthazarFights","Global",1) 
  !Exists([ENEMY])                     
THEN
  RESPONSE #100
    SetGlobal("FinalFight","AR6200",4)   
END

IF                                     //All enemies are dead, Imoen resolved, Bodhi is ally (no balthazar)
  Die()
  Global("JMT#WV1","AR6200",3)
  Global("BodhiFights","GLOBAL",1)
  !Exists([ENEMY]) 
THEN
  RESPONSE #100
    SetGlobal("FinalFight","AR6200",4)
END

IF
  Die()
THEN
  RESPONSE #100
    SetGlobal("JMT#IMD","AR6200",1)
    CreateVisualEffectObject("SPROTECT",Myself)
    ApplySpellRES("SPWI055",Myself)  // ~Disintegrate~
END

IF
  StateCheck("finiren",15)  // finiren
  OR(2)
  StateCheck("finbodh",15)  // finbodh
  Global("BodhiFights","GLOBAL",1)
  StateCheck("finsolar",15)  // finsolar
  Global("ImoenBackToHuman","GLOBAL",0)
  GlobalTimerNotExpired("ImoenChangeBack","GLOBAL")
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("ImoenBackToHuman","GLOBAL",1)
	SetGlobal("FinalFight","AR6200",4)
    ClearActions(Myself)
    ActionOverride("Imoen2",ReallyForceSpellRES("imoslay2",Myself))  // imoslay2
    ActionOverride("Imoen2",ChangeEnemyAlly("Imoen2",NEUTRAL))
    ActionOverride("Imoen2",DialogueInterrupt(TRUE))
    ActionOverride("Imoen2",ChangeAIScript("imoe25",OVERRIDE))
    ActionOverride("Imoen2",ChangeAIScript("",SPECIFICS))
    ActionOverride("Imoen2",ChangeAIScript("",CLASS))
    ActionOverride("Imoen2",ChangeAIScript("",RACE))
    ActionOverride("Imoen2",ChangeAIScript("",GENERAL))
    ActionOverride("Imoen2",ChangeAIScript("",DEFAULT))
    SetInterrupt(TRUE)
END

IF
  Global("ImoenBackToHuman","GLOBAL",0)
  GlobalTimerExpired("ImoenChangeBack","GLOBAL")
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("ImoenBackToHuman","GLOBAL",1)
	SetGlobal("FinalFight","AR6200",4)
    ClearActions(Myself)
    ActionOverride("Imoen2",ReallyForceSpellRES("imoslay2",Myself))  // imoslay2
    ActionOverride("Imoen2",ChangeEnemyAlly(Myself,NEUTRAL))
    ActionOverride("Imoen2",DialogueInterrupt(TRUE))
    ActionOverride("Imoen2",ChangeAIScript("imoe25",OVERRIDE))
    ActionOverride("Imoen2",ChangeAIScript("",SPECIFICS))
    ActionOverride("Imoen2",ChangeAIScript("",CLASS))
    ActionOverride("Imoen2",ChangeAIScript("",RACE))
    ActionOverride("Imoen2",ChangeAIScript("",GENERAL))
    ActionOverride("Imoen2",ChangeAIScript("",DEFAULT))
    SetInterrupt(TRUE)
END

IF
  Allegiance(Myself,ENEMY)
  See(NearestEnemyOf(Myself))
  !Allegiance(LastSeenBy(Myself),EVILCUTOFF)
THEN
  RESPONSE #100
    AttackReevaluate(LastSeenBy(Myself),45)
END

IF
  Allegiance(Myself,ENEMY)
  !See(NearestEnemyOf(Myself))
  !Range(Player1,15)
THEN
  RESPONSE #100
    MoveToObject(Player1)
END

IF
  Allegiance(Myself,ENEMY)
  !See(NearestEnemyOf(Myself))
  Range(Player1,15)
THEN
  RESPONSE #100
    RandomWalkContinuous()
END

